using midi;
using ui;
using dict;
using native;

const chords = {
      maj      => [0,4,7],
      maj7     => [0,4,7,11],
      maj9     => [0,4,7,11,14],
      maj11    => [0,4,7,11,14,17],
      maj13    => [0,4,7,11,14,17,21],
      maj_add2 => [0,2,4,7],
      maj_add4 => [0,4,5,7],
      maj_add9 => [0,4,7,14],
      add2     => [0,2,4,7],
      add4     => [0,4,5,7],
      add9     => [0,4,7,14],
      m        => [0,3,7],
      m6       => [0,3,7,9],
      m7       => [0,3,7,10],
      mMaj7    => [0,3,7,11],
      m9       => [0,3,7,10,14],
      m11      => [0,3,7,10,14,17],
      m13      => [0,3,7,10,14,17,21],
      m7_flat5 => [0,3,6,10],
      m7b5    => [0,3,6,10],
      aug      => [0,4,8],
      aug_maj7 => [0,4,8,11],
      aug7     => [0,4,8,10],
      aug_min7 => [0,4,8,10],
      dim      => [0,3,6],
      dim7     => [0,3,6,9],
      half_dim => [0,3,6,10],
      sixth    => [0,4,7,9],
      six_nine => [0,4,7,9,14],
      seventh        => [0,4,7,10], 
      dom7           => [0,4,7,10],
      ninth          => [0,4,7,10,14],
      eleventh       => [0,4,7,10,14,17],
      thirteenth     => [0,4,7,10,14,17,21],
      seventh_sharp9 => [0,4,7,10,15],
      seventh_flat9  => [0,4,7,10,13],
      seventh_b9     => [0,4,7,10,13],
      seventh_sharp5 => [0,4,8,10],
      seventh_flat5  => [0,4,6,10],
      seventh_b5     => [0,4,6,10],
      seventh_sus2 => [0,2,7,10],
      seventh_sus4 => [0,5,7,10],
      fifth =>  [0,7],
      sus2 =>   [0,2,7],
      sus4 =>   [0,5,7]
};


let scale =  ["C","Db","D","Eb","E","F","Gb","G","Ab","A","Bb","B"];


// Chord Degrees and supporting functions
let tonic = dict ["Maj" => chords!maj, "Maj7" => chords!maj7, "Maj9" => chords!maj9, "Maj11" => chords!maj11, "Maj13" => chords!maj13];
let tonic_keys = ["Maj", "Maj7", "Maj9", "Maj11", "Maj13"];
let tonic_chord_ref = ref 0;
tonic_chord = get tonic_chord_ref;

let supertonic = dict ["m" => chords!m, "m6" => chords!m6, "m7" => chords!m7, "m9" => chords!m9, "m11" => chords!m11];
let supertonic_keys = ["m", "m6", "m7", "m9", "m11"];
let supertonic_chord_ref = ref 0;
supertonic_chord = get supertonic_chord_ref;

let mediant = dict ["m" => chords!m, "m7" => chords!m7];
let mediant_keys = ["m", "m7"];
let mediant_chord_ref = ref 0;
mediant_chord = get mediant_chord_ref;

let subdominant = dict ["Maj "=> chords!maj, "Maj7"=> chords!maj7, "Maj9"=> chords!maj9, "Maj13"=> chords!maj13];
let subdominant_keys = ["Maj", "Maj7", "Maj9", "Maj13"];
let subdominant_chord_ref = ref 0;
subdominant_chord = get subdominant_chord_ref;

let dominant = dict ["Maj" => chords!maj, "7"=> chords!dom7, "11"=> chords!eleventh, "sus4"=> chords!sus4, "13"=> chords!thirteenth];
let dominant_keys = ["Maj", "7", "11", "sus4", "13"];
let dominant_chord_ref = ref 0;
dominant_chord = get dominant_chord_ref;

let submediant = dict ["m" => chords!m, "m7"=> chords!m7, "m9"=> chords!m9, "m11"=> chords!m11];
let submediant_keys = ["m", "m7", "m9", "m11"];
let submediant_chord_ref = ref 0;
submediant_chord = get submediant_chord_ref;

let leading = dict ["m7b5" => chords!m7b5];
let leading_keys = ["m7b5"];
let leading_chord_ref = ref 0;
leading_chord = get leading_chord_ref;


// User Interface

// Constants
const desc =
  "Choose your scale root and chorder will play a chord for each note in this scale. " +
  "You can also choose either the chorder will add a root base note to chord in a lower octave or not " +
  "by selecting Add Base Note flag. The type of the chord played for a note can be customized. " +
  "For example, in a C major scale, you can select to play C maj, C maj7, C maj11 etc. using the Tonic options, " +
  "the same goes for supertonic (D note in a C major scale) and so on";


// Dynamic variables
let transposition_ref = ref 0;
transposition = get transposition_ref;
let add_base_note_ref = ref 1;
add_base_note = get add_base_note_ref;

// UI Handlers
format_chords =
 chord_names with

    scale_note index = 
        if (index+transposition) <= 11 
        then scale ! (index+transposition)
        else scale ! (index+transposition-12); 

    tonic_c         = (scale_note 0) + ( tonic_keys ! tonic_chord );
    supertonic_c    = (scale_note 2) + ( supertonic_keys ! supertonic_chord );
    mediant_c       = (scale_note 4) + ( mediant_keys ! mediant_chord );
    subdominant_c   = (scale_note 5) + ( subdominant_keys ! subdominant_chord );
    dominant_c      = (scale_note 7) + ( dominant_keys ! dominant_chord );
    submediant_c    = (scale_note 9) + ( submediant_keys ! submediant_chord );
    leading_c       = (scale_note 11) + ( leading_keys ! leading_chord );

    chord_names = 
        tonic_c + " " + 
        supertonic_c + " " + 
        mediant_c + " " + 
        subdominant_c + " " +
        dominant_c + " " +
        submediant_c + " " +
        leading_c;
  end;

change_chords = signal (ui::code chords_control) format_chords;


// Controls, MUST be variables (defined using let keyword) for UI signals and callbacks to work
let chords_control = ui::Label "Chords" format_chords (\x -> x);
let transposition_control = ui::Segmented "Scale Tonic" transposition scale (\v -> put transposition_ref v $$ change_chords);
let scale_control = ui::Segmented "Scale" 0 ["Major", "Minor"] (\v -> v);
let tonic_control = ui::Segmented "Tonic" 0 tonic_keys (\v -> put tonic_chord_ref v $$ change_chords);
let supertonic_control = ui::Segmented "Supertonic" 0 supertonic_keys (\v -> put supertonic_chord_ref v $$ change_chords);
let mediant_control = ui::Segmented "Mediant" 0 mediant_keys (\v -> put mediant_chord_ref v $$ change_chords);
let subdominant_control = ui::Segmented "Subdominant" 0 subdominant_keys (\v -> put subdominant_chord_ref v $$ change_chords);
let dominant_control = ui::Segmented "Dominant" 0 dominant_keys (\v -> put dominant_chord_ref v $$ change_chords);
let submediant_control = ui::Segmented "Submediant" 0 submediant_keys (\v -> put submediant_chord_ref v $$ change_chords);
let leading_control = ui::Segmented "Leading" 0 leading_keys (\v -> put leading_chord_ref v $$ change_chords);
let base_note_control = ui::CheckBox "Add Base Note" add_base_note (put add_base_note_ref $$ change_chords);
let desc_control = ui::Label "Description" desc (\x -> x);
let author_control = ui::Label "Author" "Hossam Karim - (c) 2014" (\x -> x);


// UI Callback
create_ui =
  [
    transposition_control,
    //scale_control,
    base_note_control,
    chords_control,
    tonic_control,
    supertonic_control,
    mediant_control,
    subdominant_control,
    dominant_control,
    submediant_control,
    leading_control,
    desc_control,
    author_control
  ];


// Midi Processing
process_midi_buffer buf::midi_buffer_t = 
  output when
    [notes, other] = split_notes buf;
    output = (catmap process notes) + other;
  end;

split_notes xs::list = 
  foldl splitter [[],[]] xs with
     splitter [notes, other] t = 
       if is_note_on_or_off t then [notes + [t], other] else [notes, other + [t]];
  end;

process t::event_position_t = if is_note_on_or_off t then chord_on_off (pick_interval t) t else [];

chord_name t::event_position_t =
  result with
    nor = ((get_note t) - transposition) mod 12;
    name = scale ! nor;
    result =
      case nor of
        0 = name + (tonic_keys ! tonic_chord);
        2 = name + (supertonic_keys ! supertonic_chord);
        4 = name + (mediant_keys ! mediant_chord); 
        5 = name + (subdominant_keys ! subdominant_chord);
        7 = name + (dominant_keys ! dominant_chord);
        9 = name + (submediant_keys ! submediant_chord);
        11= name + (leading_keys ! leading_chord);
        _ = "";
      end;
  end;

pick_interval t::event_position_t = 
  result with
      nor = ((get_note t) - transposition) mod 12;
      name = scale ! nor;
      intervals = 
      case nor of
          0 = vals tonic ! tonic_chord;
          2 = vals supertonic ! supertonic_chord;
          4 = vals mediant ! mediant_chord;  
          5 = vals subdominant ! subdominant_chord;
          7 = vals dominant ! dominant_chord;  
          9 = vals submediant ! submediant_chord;
          11= vals leading ! leading_chord;  
          _ = debug ("No chord configured for " + name + " in current scale") $$ [];
      end;
      result = if add_base_note == 1 then (-12 : intervals) else intervals;
  end;

chord_on_off intervals::list t::event_position_t = map (transpose_note t) intervals; 

